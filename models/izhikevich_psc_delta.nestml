neuron izhikevich_psc_delta:
    
  state:
    V_m mV = -65.0 mV
    U_m mV = b * V_m
  end

  equations:
    V_m' = (0.04*V_m*V_m/mV + 5.0*V_m + (140.0 mV - U_m) + (I_e + I_stim)*GOhm)/ms
    U_m' = a*(b*V_m - U_m) / ms
  end

  parameters:
    a real = 0.02        # describes time scale of recovery variable
    b real = 0.2         # sensitivity of recovery variable
    c mV = -65 mV        # after-spike reset value of V_m
    d mV = 8.0 mV        # after-spike reset value of U_m
    I_e pA = 0.0 pA      # constant external input current
  end

  input:
    spikes mV <- spike
    I_stim pA <- continuous
  end

  output: spike

  update:
    integrate_odes()

    # Add synaptic current
    V_m += spikes

    # threshold crossing
    if V_m >= 30 mV:
      V_m = c
      U_m += d
      emit_spike()
    end
  end
end
