"""
izhikevich_psc_alpha - Detailed Izhikevich neuron model with alpha-kernel post-synaptic current
###############################################################################################

Description
+++++++++++

Implementation of the simple spiking neuron model introduced by Izhikevich [1]_, with membrane potential in (milli)volt
and current-based synapses.

The dynamics are given by:

.. math::

   C_m \frac{dV_m}{dt} = k (V - V_t)(V - V_t) - u + I + I_{syn,ex} + I_{syn,in}
   \frac{dU_m}{dt} = a(b(V_m - E_L) - U_m)

   &\text{if}\;\;\; V_m \geq V_{th}:\\
   &\;\;\;\; V_m \text{ is set to } c
   &\;\;\;\; U_m \text{ is incremented by } d

On each spike arrival, the membrane potential is subject to an alpha-kernel current of the form:

.. math::

  I_syn = I_0 \cdot t \cdot \exp\left(-t/\tau_{syn}\right) / \tau_{syn}

See also
++++++++

izhikevich, iaf_psc_alpha


References
++++++++++

.. [1] Izhikevich, Simple Model of Spiking Neurons, IEEE Transactions on Neural Networks (2003) 14:1569-1572
"""
neuron izhikevich_psc_alpha:
    state:
        V_m mV = -65 mV # Membrane potential
        U_m pA = 0 pA   # Membrane potential recovery variable

    equations:
        # synapses: alpha functions
        kernel K_syn_in = (e/tau_syn_in) * t * exp(-t/tau_syn_in)
        kernel K_syn_ex = (e/tau_syn_ex) * t * exp(-t/tau_syn_ex)

        inline I_syn_ex pA = convolve(K_syn_ex, exc_spikes)
        inline I_syn_in pA = convolve(K_syn_in, inh_spikes)

        V_m' = (k * (V_m - V_r) * (V_m - V_t) - U_m \
                + I_e + I_stim + I_syn_ex - I_syn_in) / C_m
        U_m' = a * ( b*(V_m - V_r) - U_m )

    parameters:
        C_m pF = 100 pF           # Membrane capacitance
        k pF/mV/ms = 0.7 pF/mV/ms   # Spiking slope
        V_r mV = -60 mV           # Resting potential
        V_t mV = -40 mV           # Threshold potential
        a 1/ms = 0.03 /ms         # Time scale of recovery variable
        b nS = -2 nS               # Sensitivity of recovery variable
        c mV = -50 mV             # After-spike reset value of V_m
        d pA = 100 pA              # After-spike reset value of U_m
        V_peak mV = 0 mV          # Spike detection threshold (reset condition)
        tau_syn_ex ms = 0.2 ms   # Synaptic time constant of excitatory synapse
        tau_syn_in ms = 2 ms     # Synaptic time constant of inhibitory synapse

        # constant external input current
        I_e pA = 0 pA

    input:
        inh_spikes pA <- inhibitory spike
        exc_spikes pA <- excitatory spike
        I_stim pA <- continuous

    output:
        spike

    update:
        integrate_odes()
        if V_m >= V_peak:
            V_m = c
            U_m += d
            emit_spike()
